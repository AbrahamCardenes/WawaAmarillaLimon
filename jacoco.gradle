if (!rootProject.ext.sonar.ignoreModules.contains(name)) {
    apply plugin: "jacoco"
    apply plugin: "org.sonarqube"

    def sources = "src/main/java"
    def testTask = "testDebugUnitTest"

    if (hasProperty("android")) {
        android {
            buildTypes {
                debug {
                    enableUnitTestCoverage = true
                    enableAndroidTestCoverage true
                }
            }
        }
    }

    afterEvaluate {
        tasks.withType(Test).configureEach {
            jacoco.includeNoLocationClasses = true
            jacoco.excludes = [
                    "jdk.internal.*",
                    "androidx.core.*",
                    "com.android.*",
                    "android.*",
            ]
        }

        def jacocoResults = "${buildDir}/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"

        tasks.register("testCoverage", JacocoReport) {
            dependsOn testTask

            reports {
                xml.required.set(true)
                xml.outputLocation.set(file(jacocoResults))
                html.required.set(true)
            }

            executionData.setFrom(fileTree(dir: buildDir, includes: [
                    "outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec",
                    "jacoco/testDebugUnitTest.exec"
            ]))

            sourceDirectories.setFrom(files([
                    sources
            ]))

            classDirectories.setFrom(files([
                    fileTree(
                            dir: "${buildDir}/intermediates/classes/debug/transformDebugClassesWithAsm/dirs",
                            excludes: rootProject.ext.sonar.excludeFilter
                    ),
                    fileTree(
                            dir: "${buildDir}/tmp/kotlin-classes/debug",
                            excludes: rootProject.ext.sonar.excludeFilter
                    )
            ]))
        }
    }

    sonar {
        properties {
            property "sonar.junit.reportPaths", "${buildDir}/test-results/${testTask}"
            property "sonar.coverage.jacoco.xmlReportPaths",
                    "${buildDir}/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
            property "sonar.sources", sources
        }
    }
}
