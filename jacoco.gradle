if (!rootProject.ext.sonar.ignoreModules.contains(name)) {
    apply plugin: "jacoco"
    apply plugin: "org.sonarqube"

    def sources = "src/main/java"
    def testTask = "testDebugUnitTest"

    if (hasProperty("android")) {
        android {
            buildTypes {
                debug {
                    enableUnitTestCoverage = true
                    enableAndroidTestCoverage true
                }
            }
        }
    }

    afterEvaluate {
        tasks.withType(Test).configureEach {
            jacoco.includeNoLocationClasses = true
            jacoco.excludes = [
                    "jdk.internal.*",
                    "androidx.core.*",
                    "com.android.*",
                    "android.*",
            ]
        }

        def jacocoResults = "${buildDir}/reports/jacoco/jacocoTestReport/jacocoTestReport.xml"
        def jacocoHtmlResults = "${buildDir}/reports/jacoco/jacocoTestReport/html"
      tasks.register("testCoverage", JacocoReport) {
            dependsOn testTask

            reports {
                xml.required.set(true)
                xml.outputLocation.set(file(jacocoResults))
                html.required.set(true)
                html.outputLocation.set(file(jacocoHtmlResults))
            }

            executionData.setFrom(fileTree(dir: buildDir, includes: [
                    "outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec",
                    "jacoco/testDebugUnitTest.exec"
            ]))

            sourceDirectories.setFrom(files([
                    sources
            ]))

            classDirectories.setFrom(files([
                    fileTree(
                            dir: "${buildDir}/tmp/kotlin-classes",
                            excludes: rootProject.ext.sonar.excludeFilter
                    ),
                    fileTree(
                            dir: "${buildDir}/intermediates/javac/debug/compileDebugJavaWithJavac/classes",
                            excludes: rootProject.ext.sonar.excludeFilter
                    ),
                    fileTree(
                            dir: "${buildDir}/tmp/kotlin-classes/debug",
                            excludes: rootProject.ext.sonar.excludeFilter
                    ),
                    fileTree(
                            dir: "${buildDir}/intermediates/javac/debug/classes",
                            excludes: rootProject.ext.sonar.excludeFilter
                    )
            ]))
        }
    }

    sonar {
        properties {
            property "sonar.junit.reportPaths", "${buildDir}/test-results/${testTask}"
            property "sonar.sources", sources


            property "sonar.coverage.jacoco.xmlReportPaths",
                    fileTree(rootDir)
                            .include("**/build/reports/jacoco/**/*.xml")
                            .files
                            .join(",")

            property "sonar.java.binaries",
                    fileTree(rootDir)
                            .include("**/build/intermediates/classes/**/transform*ClassesWithAsm/dirs")
                            .files
                            .join(",")


            property "sonar.java.test.binaries",
                    fileTree(rootDir) {
                        include("**/build/classes/kotlin/main")
                        include("$buildDir/intermediates/classes/debug/transformDebugClassesWithAsm/dirs")
                    }.files.join(",")
            property("sonar.sources", "src/main/java,src/main/kotlin")
        }
    }
}
