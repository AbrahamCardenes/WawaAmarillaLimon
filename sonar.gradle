ext.sonar = [
        ignoreModules: [
                "core",
        ],
        excludeFilter: [
                // add here any regex you want to ignore
                "**/test/**",
                "**/androidTest/**",
                "**/R.class",
                "**/R2.class",
                "**/R*.class",
                "**/BuildConfig.*",
                "**/Manifest*.*",
                "**/*Test*.*",
                "**/lpa_presentation/src/main/java/com/abrahamcardenes/lpa_presentation/components/*",
                "**/lpa_presentation/**/**Screen.kt",
                "**/lpa_presentation/**/components/**",
                "**/lpa_data/**/di/**",
                "**/lpa_domain/**/di/**",
                "**/core_android/**/di/**",
                "**/core_android/date/**",
                "**/core_db/src/main/java/com/abrahamcardenes/core_db/**",
                "**/DispatchersModule.kt",
                "**/lpa_presentation/**/busesInfo/busRouteDetail/SchedulesDialog.kt",
                "**/lpa_presentation/**/theme/**",
                "**/lpa_presentation/**/navigation/**",
                "**/lpa_presentation/**/GetComposeColorFrom.kt",
                "**/lpa_presentation/**/GetRandomErrorMessage.kt",
                "**/lpa_presentation/**/NotificationType.kt",
                "**/WawaAmarillaLimon/build-logic/**",
                "**/WawaAmarillaLimon.kt",
                "**/wawaamarillalimon/ui/**",
                "**/MainActivity.kt",
        ]
]

ext.sonar.ignoreModules.each {
    ext.sonar.excludeFilter << "**/${it}/**"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")

if (localPropertiesFile.exists()) {
    localProperties.load(new FileInputStream(localPropertiesFile))
}

def sonarToken = System.getenv("SONAR_TOKEN_2026") ?: localProperties.getProperty("SONAR_TOKEN")
def sonarOrganization = System.getenv("SONAR_ORGANIZATION") ?: localProperties.getProperty("SONAR_ORGANIZATION")
def sonarProjectKey = System.getenv("SONAR_PROJECT_KEY") ?: localProperties.getProperty("SONAR_PROJECT_KEY")
def sonarProjectName = System.getenv("SONAR_PROJECT_NAME") ?: localProperties.getProperty("SONAR_PROJECT_NAME")

sonar {
    properties {
        property("sonar.host.url", "https://sonarcloud.io")
        property "sonar.token", sonarToken
        property "sonar.organization", sonarOrganization
        property "sonar.projectKey", sonarProjectKey
        property "sonar.projectName", sonarProjectName
        property("sonar.sourceEncoding", "UTF-8")
        property "sonar.java.test.binaries",
                fileTree(rootDir) {
                    include("**/build/classes/kotlin/main")
                    include("$buildDir/intermediates/classes/debug/transformDebugClassesWithAsm/dirs")
                }.files.join(",")
        property("sonar.sources", "src/main/java,src/main/kotlin")

        property "sonar.coverage.jacoco.xmlReportPaths",
                fileTree(rootDir)
                        .include("**/build/reports/jacoco/**/*.xml")
                        .files
                        .join(",")

        property "sonar.java.binaries",
                fileTree(rootDir)
                        .include("**/build/intermediates/classes/**/transform*ClassesWithAsm/dirs")
                        .files
                        .join(",")
    }

}

tasks.named("sonar").configure {
    dependsOn(subprojects.collect {
        it.tasks.matching { task ->
            task.name == "jacocoTestReport"
        }
    })
}